plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    mavenCentral()
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://nexus.velocitypowered.com/repository/maven-public" }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.0.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'
    compileOnly "io.netty:netty-all:4.1.25.Final"
    compileOnly fileTree(dir: 'libraries', include: ['*.jar'])
    implementation project(":core")
}

shadowJar {
    String env = System.getProperty("env", "AUTO")
    archiveBaseName.set('MultiLogin-Velocity')
    if (env.toLowerCase() == "final") {
        archiveVersion.set(project.plugin_version as String)
    } else {
        String ver = "Build_" + ((System.currentTimeMillis() / 1000) as int)
        archiveVersion.set(ver)
    }
    archiveClassifier.set("")


    relocate 'com.zaxxer.hikari', 'moe.caa.multilogin.lib.com.zaxxer.hikari'
    relocate 'com.mysql', 'moe.caa.multilogin.lib.com.mysql'
    relocate 'com.google.gson', 'moe.caa.multilogin.lib.com.google.gson'
    relocate 'com.google.protobuf', 'moe.caa.multilogin.lib.com.google.protobuf'
    relocate 'google.protobuf', 'moe.caa.multilogin.lib.google.protobuf'
    relocate 'org.yaml.snakeyaml', 'moe.caa.multilogin.lib.org.yaml.snakeyaml'

}