plugins {
    id 'com.github.johnrengelman.shadow'
}

repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.codemc.io/repository/nms/" }
}

dependencies {
    implementation project(":core:loader")
    compileOnly group: 'com.destroystokyo.paper', name: 'paper', version: '1.16.5-R0.1-SNAPSHOT'

    // CAT_SERVER
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.2'
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.2'
    implementation group: 'me.lucko', name: 'jar-relocator', version: '1.5'
}

shadowJar {
    String env = System.getProperty("env", "AUTO")
    archiveBaseName.set('MultiLogin-Bukkit')
    if (env.toLowerCase() == "final") {
        archiveVersion.set(project.plugin_version as String)
    } else {
        String ver = "Build_" + ((System.currentTimeMillis() / 1000) as int)
        archiveVersion.set(ver)
    }
    archiveClassifier.set("")

    // CAT_SERVER
    relocate 'org.objectweb.asm', 'moe.caa.multilogin.lib.org.objectweb.asm'
    relocate 'me.lucko', 'moe.caa.multilogin.lib.me.lucko'

    from {
        project(':bukkit').tasks.shadowJar.archiveFile
    }
}

artifacts {
    archives shadowJar
}